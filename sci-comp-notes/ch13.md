
Chapter 12: Creating New Data Types in Julia
=====

[Return to all notes](../index.html)


We will start to look at playing cards (poker) probabilities below.  Before that, let's look at a different datatype in Julia that allows us to store more that one variable together.  This is often called a composite type or a struct (from C) and similar to an object however you can't have a function as a member of the type.

###Card datatype

For our playing card example, a card has a rank (1 to 13 corresponding to Ace, 2, through 10, J, Q, K) and a suit (1 to 4 corresponding to the suits  "spades", "diamonds", "hearts", "clubs"), which we define

```
ranks = ["A","2","3","4","5","6","7","8","9","T","J","Q","K"];
suits = ["\u2660","\u2661","\u2662","\u2663"]
```

where the suits are the unicode characters for the suits.   See [the julia documentation on unicode](http://docs.julialang.org/en/release-0.5/manual/unicode-input/).

We can define this using a Julia [composite type.](http://docs.julialang.org/en/release-0.5/manual/types/#composite-types).

```
type Card
    rank::Int
    suit::Int
end
```

and if we type
```
c = Card(3,2)
```

this will create a card of rank 3 and suit 2 (spades). To access the fields of the type, we use dot notation.  For example `c.rank` will return 3 and `c.suit` will return 2.  

Let's also create functions that return the rank and suit of a card:

```
function card_rank(c::Card)
    return c.rank
end
function card_suit(c::Card)
    return c.suit
end
```

Then with this typing `cardrank(c)` will return 3 and `cardsuit(c)` will return 2.  

Since this isn't very easy to read we produce a function that prints the card.

```
function print_card(c::Card)
    return string(ranks[c.rank],suits[c.suit]);
end
```

And for example typing `print_card(c)`  returns "3♡" or 3 of hearts.

When we evaluate `Card(3,2)` we  call a special function called a constructor, which creates an instance of the type.  Although Julia creates the basic contructor--that is the one that fills the fields of the type, we may want another constructor that will take an integer between 1 and 52 and returns the appropriate card.  The following example will do this.

```
type Card
    rank::Int
    suit::Int
    Card(r::Int,s::Int)=new(r,s)
    Card(i::Int) = i<1||i>52?throw(ArgumentError("The argument must be an integer between 1 and 52")):i%13==0?new(13,div(i,13)):new(i%13,div(i,13)+1)
end
```

Notice that there is a function `new` that is called.  The first constructor is the default constructor.  If a second one is made (as is in this example), you need to explicitly create it.  See [additional information about constructors in the Julia Documentation](http://docs.julialang.org/en/release-0.5/manual/constructors).

The second constructor will create a card from a number between 1 and 52 (and throw an error if it is not in this range).  We can now create a card from an integer.  For example:
```
c1=Card(45)
```

will create the 45th card in a deck and if you type: `print_card(c1)`, you will see it is the 6 of hearts.

Since a Hand is also helpful in playing card games, we will define a hand in the following way:
```
type Hand
    cards::Array{Card,1}
end
```

which is just an array of cards.  (Note: there is nothing here that specifies that the Hand has to be 5 cards, but that could be included by doing some error checking in the constructor)


Let's create a Hand by typing
```
h=Hand([Card(2,3),Card(12,1),Card(10,1),Card(10,4),Card(5,2)])
```

and then we can print out a hand by defining a `print_hand` function
```
function print_hand(h::Hand)
    join(map(print_card,h.cards),",")
end
```

and with the above hand:
```
print_hand(h)
```

returns "2♢,Q♠,T♠,T♣,5♡"

We now have some nice additional types, however, we now want to do something interesting with them.


###Find Probabilities of Poker Hands

Let's actually look at determining if a hand is a particular poker hand.  Some of them are fairly difficult to test for. Let's first try a full house, which is a hand of 5 cards, 2 of which have the same rank and 3 of which have the same rank.  The two ranks are not the same. For more information about poker hands, see the [Wikipedia page on Poker Hands](https://en.wikipedia.org/wiki/List_of_poker_hands).

A template for the `is_full_house` function is as follows.  

```
function is_full_house(h::Hand)

end
```

Once we have the function written, we should test it on a few known and unknown full house hands.  Try testing:

```
fh1= Hand(Card[Card(4,1),Card(4,3),Card(4,4),Card(7,1),Card(7,2)])
```

```
fh2=Hand([Card(4,1),Card(4,3),Card(7,4),Card(7,1),Card(7,2)])
```

and

```
fh3=Hand([Card(2,1),Card(4,3),Card(4,4),Card(7,1),Card(7,2)])
```

And note that the first 2 are full house hands and the last is not.

#### Running a Simulation

Now, let's perform a Monte Carlo simulation on a large number of poker hands and test if this gives the result we want:

```
deck=collect(1:52) # creates the array [1,2,3,...,52]
numhands=0
trials = 100_000
for i=1:trials
    shuffle!(deck)
    h = Hand(map(Card,deck[1:5]))  # creates a hand back on the first five cards of the deck
    if(is_full_house(h))
        numhands+=1
    end
end
numhands/trials
```

will create 100,000 poker hands and test how many are full house.  It would be helpful to put this in a function.  We will do this and put all of these in a package together in the next chapter.
