
Chapter 14: Creating A Module and Performing Unit Tests
=====

[Return to all notes](../index.html)

In [Chapter 7](ch07.html) we learned about adding and loading packages or modules.  These were generally official ones. In this section, we will now learn how to write our own module.  We will learn how to do this by creating a `PlayingCards` module with all of the types and functions associated with it.  It is helpful to read through the [julia documentation on modules](http://docs.julialang.org/en/latest/manual/modules/).

To begin with, let's look at a module template:

```
module PlayingCards

export Card,print_card,Hand,print_hand,is_full_house

##definitions of types and functions

end
```

As you can see above, a module has a name (in this case `PlayingCards`) and ends with the keyword `end`. We will next put a number of types and functions inside the module, but in order for someone loading the module, you need to export anything to be used.

The following is a more detailed version of the module:

```
module PlayingCards

export Card,print_card,Hand,print_hand,is_full_house

ranks = ['A','2','3','4','5','6','7','8','9','T','J','Q','K']
suits = ['\u2660','\u2661','\u2662','\u2663']


type Card
    rank::Int
    suit::Int

    # construct a card based on the rank and suit
    function Card(r::Int,s::Int)
        if r<1||r>13
            throw(ArgumentError("The rank must be an integer between 1 and 13."))
        end
        if s<1||s>4
            throw(ArgumentError("The suit must be an integer between 1 and 4."))
        end
        new(r,s)
    end

    # construct a card based on the number in a deck
    function Card(i::Int)
        if i<1||i>52
            throw(ArgumentError("The argument must be an integer between 1 and 52"))
        end
        if i%13==0
            new(13,div(i,13))
        else
            new(i%13,div(i,13)+1)
        end
    end

    # construct a card based on a string representation of the card
    function Card(str::String)
        if length(str)>2
            throw(ArgumentError("The string should only be 2 characters"))
        end
        local r = findfirst(a->a==str[1],ranks)
        if r<1 || r>13
            throw(ArgumentError(string("The first character should be one of ",join(ranks,","))))
        end
        local s=findfirst(a->a==str[2],suits)
        if s<1 || s>4
            throw(ArgumentError(string("The second character should be one of ",join(suits,","))))
        end
        new(r,s)
    end

end

function print_card(c::Card)
    string(ranks[c.rank],suits[c.suit]);
end

function card_rank(c::Card)
    return c.rank
end

function card_suit(c::Card)
    return c.suit
end

type Hand
    cards::Array{Card,1}
    Hand(c::Array{Card,1})=new(c)
    Hand(c::Array{String,1})=new(map(Card,c))
    Hand(s::String)=new(map(Card,map(String,split(s,','))))
end

function print_hand(h::Hand)
    join(map(print_card,h.cards),",")
end

function is_full_house(h::Hand)
    local cranks=sort(map(card_rank,h.cards))
    return cranks[2]==cranks[1] && cranks[5]==cranks[4] && (cranks[3]==cranks[2] || cranks[4]==cranks[3]);
end

end
```

Open a new file in jupyter or juliabox and copy-paste the above module into the empty file.  It will need to be called `PlayingCards.jl`.

#### Changing the LOAD_PATH variable

Julia looks for modules in a few directories on your computer.  If you type
```
LOAD_PATH
```  

in an empty ipynb file, it will return an array of directories where the modules are located.  The module that you wrote however, is probably in the same directory as the ipynb files.  If this is going to be the situation, typing
```
push!(LOAD_PATH, ".")
```

will add the "." directory (current one) to the `LOAD_PATH` array.  Then julia will be able to find it.

#### Loading the module

Next, we will load the module in as before.

```
using PlayingCards
```

will now load in the module.  

#### Troubleshooting

* If you got an error when using `using PlayingCards` read carefully what the error is.  If you got: `ArgumentError: Module PlayingCards not found in current path.` make sure that the load path has the directory it is in.

* If you


### Best practices for creating modules

* You should test all of your code before putting it in a module.  It's difficult to debug once it is in a module.

* Typing `using PlayingCards` does not reload all of the code.  If you need to reload it, you should type `include("PlayingCards")`.

* The above is supposed, but I have found doesn't always work.  You may need to do a `workspace()` command and reload using `using PlayingCards`.  

* Make sure that you `export` only types and functions that you want to make available.  Notice in the example above, we have variables called `ranks` and `suits` that are not exported and do not need to be exported.  

## Unit Tests

Writing a module is important, but making sure it does what it is supposed to is just as important.  The notion of a unit test is a set of tests to determine if you get out from the code what you expect.  To run a test, first import the `Base.Test` package:
```
using Base.Test
```

To do a test, we'll use the `@test` macro.  For this module, let's first test that the constructor is working and for this we will need a way to test if two Cards are equal.

```
function isequal(x::Card,y::Card)
  x.rank==y.rank&&x.suit==y.suit
end
```

then create two cards
```
c1=Card(4,1)
```

```
c2=Card(4)
```

and test if they are the same with
```
@test isequal(c1,c2)
```

and let's also check if some cards deeper in the deck are working:

```
c3=Card(7,3);
c4=Card(33);
```

```
@test isequal(c3,c4)
```

#### Test if an exception is thrown

It's also important to test if an exception is thrown for creating illegal cards.  For example, a `Card(0,3)` does not exist and will throw an exception (try it!).  To test this,
```
@test_throws ArgumentError Card(0,3)
```

returns

```
Test Passed
  Expression: Card(0,3)
      Thrown: ArgumentError
```


### Putting all tests in a file

The [Playing Card test file](test-playing-cards.jl) is a better way to run a set of tests.  To use this, download the file, put it in the current directory of juliabox or jupyter and then run it with:

```
include("test-playing-cards.jl")
```

and you should see:

```
Test Summary:      | Pass  Total
  Playing Card Tests |    5      5
```

printed out.  
